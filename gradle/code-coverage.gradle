/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 */

apply plugin: 'java'
apply plugin: 'jacoco'

repositories {
  mavenCentral()
}

jacoco {
  toolVersion = "0.8.7"
}

task jacocoMergedReport(type: JacocoReport, group: 'verification') {
  description = 'Generates aggregate report from all subprojects.'
  executionData.setFrom fileTree(dir: '.', include: '**/build/jacoco/*.exec')
  gradle.projectsEvaluated {
    def selectedSubprojects = []
    subprojects.each {
      if (it.tasks.findByPath('test') && it.tasks.findByPath('test').enabled == true && it.sourceSets.findByName("main")) {
        selectedSubprojects.add(it)
      }
    }
    sourceDirectories.setFrom files(selectedSubprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(selectedSubprojects.sourceSets.main.output)
  }
  reports {
    xml.enabled true
    html.enabled true
  }
}

