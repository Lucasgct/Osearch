/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 */

apply plugin: 'jacoco'

repositories {
  mavenCentral()
}

allprojects {
  plugins.withId('jacoco') {
    jacoco.toolVersion = '0.8.7'
  }
}

task codeCoverageReport(type: JacocoReport, group: 'verification') {
  description = 'Generates aggregate report from all subprojects.'

  executionData.setFrom fileTree(dir: '.', include: '**/build/jacoco/*.exec')
  gradle.projectsEvaluated {
    def selectedSubprojects = []
    subprojects.each {
      if (it.pluginManager.hasPlugin('java') && it.tasks.findByName('test').enabled) {
        selectedSubprojects.add(it)
      }
    }
    sourceDirectories.setFrom files(selectedSubprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(selectedSubprojects.sourceSets.main.output)
    dependsOn selectedSubprojects.test
  }
  reports {
    xml.enabled true
    html.enabled true
  }
}
